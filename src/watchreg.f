
: REG-START-NOTIFY ( -- ior )
    1 CUR-WATCH WATCH-HANDLE @
    CUR-WATCH WATCH-PAR @ ?DUP 0=
    IF
    [   REG_NOTIFY_CHANGE_NAME
        REG_NOTIFY_CHANGE_ATTRIBUTES OR
        REG_NOTIFY_CHANGE_LAST_SET OR
        REG_NOTIFY_CHANGE_SECURITY OR ] LITERAL
    THEN
    Subtree? CUR-WATCH WATCH-PAR1 @ RegNotifyChangeKeyValue
    DUP
    IF DUP TO WATCH-ERR S" RegNotifyChangeKeyValue error: %WATCH-ERR WinErrorMessage%" LOG-WATCH
        CUR-WATCH WATCH-HANDLE @ CloseHandle DROP CUR-WATCH WATCH-PAR1 @ RegCloseKey DROP
    THEN
;

: WATCH-REG-START ( a u -- handle)
    WATCH-OBJECT-S!
    CUR-WATCH WATCH-PAR1 KEY_NOTIFY 0
    WATCH-OBJECT@ FALSE REG-PARSE ( branch apath aval)
    DROP
\    DBG( ." WATCH-REG-START subkey: " DUP ASCIIZ> TYPE CR )
    SWAP RegOpenKeyExA
\    DBG( ." WATCH-REG-START RegOpenKeyExA=" DUP . CR )
    ?DUP IF TO WATCH-ERR S" RegOpenKeyExA error: %WATCH-ERR WinErrorMessage%" LOG-WATCH 0 EXIT THEN

    Event CUR-WATCH WATCH-HANDLE !

    REG-START-NOTIFY
    IF 0 ELSE CUR-WATCH WATCH-HANDLE @ THEN
;

: WATCH-REG-CONTINUE
    REG-START-NOTIFY DROP
;

: WATCH-REG-STOP
    CUR-WATCH WATCH-PAR1 @ RegCloseKey
    DBG( ." WATCH-REG-STOP=" DUP . CR )
    DROP
;

: WatchRegistryKey:
    POSTPONE WATCH:
    WNEED-LOGON
    eval-string, POSTPONE WATCH-REG-START
    ['] WATCH-REG-STOP  WATCH-NODE WATCH-STOP !
    ['] WATCH-REG-CONTINUE WATCH-NODE WATCH-CONTINUE !
    POSTPONE END-WATCH
;  IMMEDIATE

REG_NOTIFY_CHANGE_NAME          WATCH-PAR:  REG-CHANGE-NAME
REG_NOTIFY_CHANGE_ATTRIBUTES    WATCH-PAR:  REG-CHANGE-ATTRIBUTES
REG_NOTIFY_CHANGE_LAST_SET      WATCH-PAR:  REG-CHANGE-LAST-SET
REG_NOTIFY_CHANGE_SECURITY      WATCH-PAR:  REG-CHANGE-SECURITY
