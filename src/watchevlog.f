REQUIRE EVLOG-OPEN ~nn/lib/win/sys/evlog.f

: WATCH-EVLOG-START ( a u -- handle )
    WATCH-OBJECT-S!
    WATCH-OBJECT@ EVLOG-OPEN IF 0 EXIT THEN
    DUP >R CUR-WATCH WATCH-PAR1 !   \ event log handle

    R@ EVLOG-NEWEST THROW CUR-WATCH WATCH-PAR2 ! \ latest record

    Event DUP R@ NotifyChangeEventLog
    0= IF CloseHandle DROP R@ EVLOG-CLOSE DROP 0 THEN
    RDROP
;
\ WATCH-PAR3 - list of events
: WATCH-EVLOG-RULE { \ flag ev -- ?}
\    TRUE EXIT
    FALSE TO flag
    GLOBAL
    [NONAME NodeValue FREE DROP NONAME] CUR-WATCH WATCH-PAR3 DoList
    CUR-WATCH WATCH-PAR3 FreeList
    CUR-WATCH WATCH-PAR3 0!
    CUR-WATCH WATCH-PAR2 @ ( latest_record_number -- )
\    DBG( ." EVENT LATEST RECORD NUMBER = " CUR-WATCH WATCH-PAR2 @ . CR )
\    .S
    BEGIN
        1+ DUP CUR-WATCH WATCH-PAR1 @ EVLOG-READ ( .S) 0=
    WHILE
        TO ev
        ev evEventType W@ CUR-WATCH WATCH-PAR @ AND 0<>
        CUR-WATCH WATCH-PAR @ 0= OR
        IF
            TRUE TO flag
\            DBG( ." event log: event type " ev evEventType W@ . CR ev DUP @ DUMP CR )
            ev CUR-WATCH WATCH-PAR3 AppendNode
        ELSE
            ev FREE DROP
        THEN
    REPEAT
    DROP
\    .S
    1- CUR-WATCH WATCH-PAR2 !
    LOCAL
    flag
;
USER <FOUND-EVENT>
: FOUND-EVENT <FOUND-EVENT> @ ;
: set-found-event ( a -- ) NodeValue <FOUND-EVENT> ! ;
: FOR-NEW-EVENTS POSTPONE [NONAME POSTPONE set-found-event ; IMMEDIATE
: (FOR-NEW-EVENTS) vTask vtWATCH @ ?DUP IF WATCH-PAR3 DoList ELSE DROP THEN ;
: ;FOR-NEW-EVENTS POSTPONE NONAME] POSTPONE (FOR-NEW-EVENTS) ; IMMEDIATE

: WATCH-EVLOG-STOP
    CUR-WATCH WATCH-PAR1 @ CloseEventLog
\    DBG( ." WATCH-EVLOG-STOP=" DUP . CR )
    DROP
;

: WatchEventLog:
    POSTPONE WATCH:
    eval-string, POSTPONE WATCH-EVLOG-START
    ['] WATCH-EVLOG-STOP  WATCH-NODE WATCH-STOP !
    ['] WATCH-EVLOG-RULE  WATCH-NODE WATCH-RULE !
    POSTPONE END-WATCH
;  IMMEDIATE

: EVENTLOG-ERROR-TYPE EVENTLOG_ERROR_TYPE               OR-WATCH-PAR ; IMMEDIATE
: EVENTLOG-WARNING-TYPE EVENTLOG_WARNING_TYPE           OR-WATCH-PAR ; IMMEDIATE
: EVENTLOG-INFORMATION-TYPE EVENTLOG_INFORMATION_TYPE   OR-WATCH-PAR ; IMMEDIATE
: EVENTLOG-AUDIT-SUCCESS EVENTLOG_AUDIT_SUCCESS         OR-WATCH-PAR ; IMMEDIATE
: EVENTLOG-AUDIT-FAILURE EVENTLOG_AUDIT_FAILURE         OR-WATCH-PAR ; IMMEDIATE
